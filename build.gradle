plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id "org.openapi.generator" version "6.4.0"
    id "io.freefair.lombok" version "6.6.3"
}

group = 'com.malishnya'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor('org.hibernate:hibernate-jpamodelgen:6.1.7.Final')
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.8'
    implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.6'
    implementation group: 'org.modelmapper', name: 'modelmapper', version: '3.1.1'
    compileOnly 'org.openapitools:openapi-generator-gradle-plugin:6.4.0'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

sourceSets {
    getByName("main") {
        java {
            srcDir("$buildDir/generated/src/main/java")
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/docs/api.yml".toString()
    outputDir = "$buildDir/generated".toString()
    packageName = "com.malishnya.moretech.api"
    apiPackage = "com.malishnya.moretech.api.generated.api"
    modelPackage = "com.malishnya.moretech.api.generated.model"
    configOptions = [
            dateLibrary   : "java8-localdatetime",
            interfaceOnly : "true",
            useTags       : "true",
            useSpringBoot3: "true"
    ]
}